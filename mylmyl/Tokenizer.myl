module Tokenizer;

import Util;

export enum TokenType where
    LPAREN(), RPAREN(), LBRACKET(), RBRACKET(), LBRACE(), RBRACE(), SEMICOLON(),
    DOT(), DOTS(), COMMA(), COLON(), ASSIGN(), PLUS(), MINUS(), STAR(), SLASH(), PERCENT(), BANG(), EXPO(),
    LESS(), LESS_EQUAL(), GREATER(), GREATER_EQUAL(), EQUAL(), NOT_EQUAL(),
    QUESTION_MARK(),

    END(), WHERE(), DO(),

    NULL(), TRUE(), FALSE(), STRING_LITERAL(), NUMBER_LITERAL(),

    AND(), OR(), XOR(),
    
    PRINT(),

    IDENTIFIER(),

    // Value kexwords
    VAR(), VAL(), VALUE_THIS(), NEW(), FUN(), IF(), THEN(), ELSE(), WHILE(), FOR(), IN(), YIELD(),
    RETURN(), MATCH(), CASE(),

    // Namespacing Keywords
    MODULE(), IMPORT(), EXPORT(), LOCAL(),

    // Type Keywords
    CLASS(), TYPE_FUN(), NUMBER(), BOOLEAN(), STRING(), VOID(), TYPE(), ENUM(),


    EOF(), ERROR();
end

export class Token where 
    val kind : TokenType;
    val src : String;
    val pos : SourcePos;
    new(k : TokenType, s : String, p : SourcePos) do 
        this.kind := k;
        this.src := s;
        this.pos := p;
    end
end;

class Tokenizer where 
    val source : String;
    var position : Number;

    var sourcePos : SourcePos;

    new(s : String) do 
        this.source := s;
        this.position := 0;
        this.sourcePos := SourcePos(0, 0);
    end;

    fun peek() : String := this.source[this.position];
    fun peekNext() : String := this.source[this.position+1];
    fun next() : String do 
        val c := this.peek();
        this.position := this.position + 1;
        this.sourcePos := nextPos(c, this.sourcePos);
        c
    end

    fun consume(str : String) do
        val actual := this.next();
        if actual != str do 
            panic("Expected "+str+", got "+actual);
        end
    end

    fun at_end() : Bool := this.position >= len(this.source);

    fun charsLeft() : Number := len(this.source) - this.position;

    fun tokenize() : Token[] do 
        val tokens : Token[] := [];
        while !this.at_end() do 
            this.parse_token(tokens);
        end;
        tokens.push(Token(EOF(), "", this.sourcePos));
        tokens
    end

    fun parse_token(ls : Token[]) do 
        val c := this.next();
        if strip(c) = "" do // Skip whitespace
            return null;
        end
        match c do 
            case "(" := ls.push(Token(LPAREN(), "(", this.sourcePos));
            case ")" := ls.push(Token(RPAREN(), ")", this.sourcePos));
            case "[" := ls.push(Token(LBRACKET(), "[", this.sourcePos));
            case "]" := ls.push(Token(RBRACKET(), "]", this.sourcePos));
            case "{" := ls.push(Token(LBRACE(), "{", this.sourcePos));
            case "}" := ls.push(Token(RBRACE(), "}", this.sourcePos));
            case ";" := ls.push(Token(SEMICOLON(), ";", this.sourcePos));
            case ":" := if this.peek() = "=" then do 
                this.consume("=");
                ls.push(Token(ASSIGN(), ":=", this.sourcePos));                
            end else do 
                ls.push(Token(COLON(), ":", this.sourcePos));
            end;
            case "." := if this.peek() = "." then do 
                this.consume(".");
                ls.push(Token(DOTS(), "..", this.sourcePos));
            end else do
                ls.push(Token(DOT(), ".", this.sourcePos));
            end;
            case "," := ls.push(Token(COMMA(), ",", this.sourcePos));
            case "+" := ls.push(Token(PLUS(), "+", this.sourcePos));
            case "-" := ls.push(Token(MINUS(), "-", this.sourcePos));
            case "*" := ls.push(Token(STAR(), "*", this.sourcePos));
            case "/" := do 
                val nxt := this.peek();
                if nxt = "/" or nxt = "*" then do
                    this.comment();
                end else do 
                    ls.push(Token(SLASH(), "/", this.sourcePos));
                end;
            end;
            case "%" := ls.push(Token(PERCENT(), "%", this.sourcePos));
            case "^" := ls.push(Token(EXPO(), "^", this.sourcePos));
            case "<" := if this.peek() = "=" then do 
                this.consume("=");
                ls.push(Token(LESS_EQUAL(), "<=", this.sourcePos));
            end else do 
                ls.push(Token(LESS(), "<", this.sourcePos));
            end;
            case ">" := if this.peek() = "=" then do 
                this.consume("=");
                ls.push(Token(GREATER_EQUAL(), ">=", this.sourcePos));
            end else do 
                ls.push(Token(GREATER(), ">", this.sourcePos));
            end;
            case "=" := ls.push(Token(EQUAL(), "=", this.sourcePos));
            case "!" := if this.peek() = "=" then do 
                this.consume("=");
                ls.push(Token(NOT_EQUAL(), "!=", this.sourcePos));
            end else do 
                ls.push(Token(BANG(), "!", this.sourcePos));
            end;
            case "?" := ls.push(Token(QUESTION_MARK(), "?", this.sourcePos));
            case "\"" := do 
                var content := "";
                var running := true;
                while !this.at_end() and running do 
                    val next := this.next();
                    if next = "\"" then do 
                        running := false;
                    end else if next = "\\" then do 
                        match this.next() do 
                            case "\\" := do 
                                content := content + "\\";
                            end;
                            case "\"" := do 
                                content := content + "\"";
                            end;
                            case "n" := do 
                                content := content + "\n";
                            end;
                            case "t" := do 
                                content := content + "\t";
                            end;
                            case "r" := do 
                                content := content + "\r";
                            end;
                            case "0" := do 
                                content := content + "\0";
                            end;
                            case c := do 
                                content := content + c;
                            end;
                        end;
                    end else do 
                        content := content + next;
                        true;
                    end;
                end;
                ls.push(Token(STRING_LITERAL(), content, this.sourcePos));
            end;
            
            case c := 
                if matches(c, "[a-zA-Z]|_") then do 
                    var ident := c;
                    while !this.at_end() and matches(this.peek(), "[a-zA-Z0-9]|_") do 
                        ident := ident + this.next();
                    end;
                    ls.push(Token(this.identifierToken(ident), ident, this.sourcePos));
                end else if matches(c, "[0-9]") then do 
                    var n := c;
                    while !this.at_end() and matches(this.peek(), "[0-9]") do 
                        n := n + this.next();
                    end;
                    if this.charsLeft() >= 2 and this.peek() = "." and matches(this.peekNext(), "[0-9]") do 
                        n := n + "."; this.next();
                        while !this.at_end() and matches(this.peek(), "[0-9]") do 
                            n := n + this.next();
                        end;
                    end;
                    ls.push(Token(NUMBER_LITERAL(), n, this.sourcePos));
                end else do 
                    panic("Invalid character: "+c);
                end;
        end;
    end

    fun identifierToken(name : String) : TokenType := match name do 
        case "where" := WHERE();
        case "end" := END();
        case "true" := TRUE();
        case "false" := FALSE();
        case "null" := NULL();
        case "not" := BANG();
        case "this" := VALUE_THIS();
        case "new" := NEW();
        case "if" := IF();
        case "then" := THEN();
        case "else" := ELSE();
        case "while" := WHILE();
        case "for" := FOR();
        case "in" := IN();
        case "do" := DO();
        case "yield" := YIELD();
        case "and" := AND();
        case "or" := OR();
        case "xor" := XOR();
        case "var" := VAR();
        case "val" := VAL();
        case "fun" := FUN();
        case "class" := CLASS();
        case "module" := MODULE();
        case "import" := IMPORT();
        case "export" := EXPORT();
        case "local" := LOCAL();
        case "type" := TYPE();
        case "return" := RETURN();
        case "enum" := ENUM();
        case "match" := MATCH();
        case "case" := CASE();
        case "Fun" := TYPE_FUN();
        case "Number" := NUMBER();
        case "Bool" := BOOLEAN();
        case "String" := STRING();
        case "Void" := VOID();
        case "print" := PRINT();
        case id := IDENTIFIER();
    end;

    fun comment() do 
        val c := this.next();
        if c = "/" then do 
            while this.next() != "\n" do end;
        end else do 
            var ident_level := 1;
            while ident_level > 0 do 
                val nxt := this.next();
                if nxt = "/" then do 
                    if this.next() = "*" do 
                        ident_level := ident_level + 1;
                    end;
                end else if nxt = "*" then do
                    if this.next() = "/" do
                        ident_level := ident_level - 1;
                    end;
                end else do end;
            end;
        end;
    end
end;

class SourcePos where 
    val line : Number;
    val collumn : Number;
    new(l : Number, c : Number) do 
        this.line := l; this.collumn := c;
    end
end

fun nextPos(c : String, pos : SourcePos) : SourcePos := 
    if c = "\n" then 
        SourcePos(pos.line + 1, 0)
    else 
        SourcePos(pos.line, pos.collumn + 1);

fun getContent(content: String): Util.Optional(String) do
    val fileContent := openFile(content);
    if isNull(fileContent) then Util.None.[String]() else Util.Some.[String](fileContent)
end


fun main() do 
    val Some(input) := getContent("mylmyl/Tokenizer.myl") else do 
        panic("Could not open file")
    end;
    val tokenized := tokenize(input);
    for elem in tokenized do 
        print(elem);
    end;
end
// test
export fun tokenize(input : String) : Token[] do 
    val t := Tokenizer(input);
    t.tokenize()
end 

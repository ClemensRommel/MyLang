module Parser;

import Tokenizer;
import Ast;
import Util;
import Env;

class Parser where 
    val tokens : Tokenizer.Token[];
    var pos : Number;

    val to_be_desugared : Ast.MutExpr[];

    new(source : String) do
        this.tokens := Tokenizer.tokenize(source);
        this.pos := 0;
    end;

    fun next() : Tokenizer.Token do 
        val token := this.tokens[this.pos];
        this.pos := this.pos + 1;
        token
    end

    fun peek() : Tokenizer.Token := this.tokens[this.pos];
    fun peekNext() : Tokenizer.Token := this.tokens[this.pos+1];

    fun consume(t : Tokenizer.TokenType, error : String) : Tokenizer.Token do 
        val nxt := this.next();
        if nxt.kind != t do
            panic("["+nxt.pos.line+":"+nxt.pos.collumn+"]: Unexpected "+nxt.kind+": "+error);
        end;
        nxt
    end

    fun matches(t : Tokenizer.TokenType) : Bool do 
        if this.peek().kind = t then do 
            this.pos := this.pos + 1;
            true
        end else do 
            false
        end
    end

    fun previous() : Tokenizer.Token := this.tokens[this.pos - 1];
    fun at_end() : Bool := this.peek().kind = Tokenizer.EOF();

    fun wrap_sugared(e : Ast.Expr) : Ast.Expr := do 
        val mut_expr := Ast.MutExpr(e);
        this.to_be_desugared.push(mut_expr);
        Ast.DesugarableExpr(mut_expr)
    end;
end;

export fun parse(source : String) : Ast.Program do 
    val parser := Parser(source);
    program(parser)
end

fun program(p : Parser) : Ast.Program do 
    val prgm := Ast.Program();
    while !p.at_end() do 
        prgm.decls.push(decl(p));
    end;
    prgm
end

fun stmt(p : Parser) : Ast.Stmt do 
    val nxt := p.peek();
    if is_decl_start(p) do 
        return Ast.Declaration(decl(p));
    end;
    if val PRINT() := nxt.kind then do 
       p.next();
       val arg := expr(p);
       p.consume(Tokenizer.SEMICOLON(), "Expect ';' after print statement");
       Ast.PrintStmt(arg)
    end else if val SEMICOLON() := nxt.kind then do 
        Ast.EmptyStmt()
    end else do 
        val arg := expr(p);
        if p.matches(Tokenizer.ASSIGN()) then do 
            assign_stmt_to(p, arg)
        end else do 
            p.consume(Tokenizer.SEMICOLON(), "Expect ';' after expression statement");
            Ast.ExprStmt(arg)
        end
    end
end

fun is_decl_start(p : Parser) : Bool := match p.peek().kind do 
    case VAL() := true;
    case VAR() := true;
    case FUN() := p.peekNext().kind = Tokenizer.IDENTIFIER();
    case CLASS() := true;
    case ENUM() := true;
    case ? := false;
end;

fun decl(p : Parser) : Ast.Decl do 
    val decl_kind := p.next().kind;
    match decl_kind do 
        case VAR() := var_decl(p, true);
        case VAL() := var_decl(p, false);
        case FUN() := fun_decl(p);
        case CLASS() := class_decl(p);
        case ENUM() := enum_decl(p);
    end
end

fun enum_decl(p : Parser) : Ast.Decl do 
    val name := p.consume(Tokenizer.IDENTIFIER(), "Expect class name after 'class' keyword");
    p.consume(Tokenizer.WHERE(), "Expect start of enum body after enum name: 'where'");
    val constructors : Ast.EnumConstructor[] := [];
    var needs_comma := false;
    while !p.matches(Tokenizer.SEMICOLON()) do 
        if needs_comma do 
            p.consume(Tokenizer.COMMA(), "Expect ',' to separate enum variants");
        end;
        val name := p.consume(Tokenizer.IDENTIFIER(), "Expect enum variant to start with identifier");
        p.consume(Tokenizer.LPAREN(), "Expect '(' after enum constructor name");
        val types : Ast.Type[] := [];
        var inner_needs_comma := false;
        while !p.matches(Tokenizer.RPAREN()) do 
            if inner_needs_comma do p.consume(Tokenizer.COMMA(), "Expect ',' to separate enum variant parameters"); end;
            types.push(type_literal(p));
            inner_needs_comma := true;
        end;
        constructors.push(Ast.EnumConstructor(name.src, types));
        needs_comma := true;
    end;
    val members : Ast.Decl[] := [];
    while !p.matches(Tokenizer.END()) do 
        members.push(decl(p));
    end;
    Ast.EnumDecl(name.src, constructors, members, true, Ast.late_env())
end

fun class_decl(p : Parser) : Ast.Decl do 
    val name := p.consume(Tokenizer.IDENTIFIER(), "Expect class name after 'class' keyword");
    p.consume(Tokenizer.WHERE(), "Expect start of class body after class name: 'where'");
    val members : Ast.Decl[] := [];
    var constr : Util.Optional(Ast.Constructor) := Util.None.[Ast.Constructor]();
    while !p.matches(Tokenizer.END()) do 
        if p.matches(Tokenizer.NEW()) then do 
            if val Some(?) := constr then do 
                panic("Error: Class '"+name.src+"' can only have on constructor");
            end else do end;
            constr := Util.Some.[Ast.Constructor](constructor(p));
        end else do 
            val member : Ast.Decl := decl(p);
            members.push(member);
        end;
    end;
    Ast.ClassDecl(name.src, members, constr, true, Ast.late_env())
end

fun constructor(p : Parser) : Ast.Constructor do 
    val params := parse_parameters(p);
    p.consume(Tokenizer.DO(), "Expect do after end of constructor parameter declaration");
    val body := block_expr(p);
    Ast.Constructor(params, body)
end

fun fun_decl(p : Parser) : Ast.Decl do 
    val name := p.consume(Tokenizer.IDENTIFIER(), "Expect name in function declaration");
    val (params, return_type, body) := end_of_function(p, true);
    Ast.FunDecl(name.src, params, return_type, body, true, Ast.late_env())
end

fun parse_parameters(p : Parser) : Ast.Parameter[] do 
    p.consume(Tokenizer.LPAREN(), "Expect start of parameters in function declaration");
    val params : Ast.Parameter[] := [];
    while !p.matches(Tokenizer.RPAREN()) do 
        params.push(parameter(p));
        if p.peek().kind != Tokenizer.RPAREN() do 
            p.consume(Tokenizer.COMMA(), "Expect ',' between parameters");
        end;
    end;
    params
end

fun end_of_function(p : Parser, consume_semi : Bool) : (Ast.Parameter[], Ast.Type, Ast.Expr) do
    val params := parse_parameters(p);
    p.consume(Tokenizer.COLON(), "Expect ':' at start of function return type");
    val return_type := type_literal(p);
    val body := if p.matches(Tokenizer.ASSIGN()) then do
        val b := expr(p);
        if consume_semi do 
            p.consume(Tokenizer.SEMICOLON(), "Expect ';' after function body");
        end
        b 
    end else do 
        p.consume(Tokenizer.DO(), "Expect ':=' or 'do' as start of function body");
        block_expr(p)
    end;
    (params, return_type, body)
end

fun parameter(p : Parser) : Ast.Parameter do 
    val name := p.consume(Tokenizer.IDENTIFIER(), "Expect parameter name");
    p.consume(Tokenizer.COLON(), "Expect ':' before parameter type");
    val param_type := type_literal(p);
    Ast.NormalParam(name.src, param_type)
end

fun var_decl(p : Parser, mutable : Bool) : Ast.Decl do 
    val setter := convert_setter(expr(p));
    val ty := Ast.late_type();
    if p.matches(Tokenizer.COLON()) do 
        ty.init(type_literal(p));
    end;
    val initializer := if p.matches(Tokenizer.ASSIGN()) then do 
        Util.Some.[Ast.Expr](expr(p))
    end else Util.None.[Ast.Expr]();
    p.consume(Tokenizer.SEMICOLON(), "Expect ';' after variable declaration");
    Ast.VarDecl(setter, ty, initializer, mutable, true)
end

fun expr(p : Parser) : Ast.Expr := boolean_combination(p);

fun boolean_combination(p : Parser) : Ast.Expr do 
    var lhs := comparison(p);
    while p.matches(Tokenizer.AND()) or p.matches(Tokenizer.OR()) or p.matches(Tokenizer.XOR()) do 
        val operator := p.previous();
        val rhs := comparison(p);
        val op := match operator.kind do 
            case AND() := Ast.And();
            case OR() := Ast.Or();
            case XOR() := Ast.Xor();
        end;
        lhs := Ast.BinaryOperation(op, lhs, rhs); 
    end;
    lhs
end

fun comparison(p : Parser) : Ast.Expr do 
    var result := addition(p);
    if p.matches(Tokenizer.LESS()) or p.matches(Tokenizer.LESS_EQUAL()) or p.matches(Tokenizer.EQUAL())
        or p.matches(Tokenizer.GREATER_EQUAL()) or p.matches(Tokenizer.GREATER()) or p.matches(Tokenizer.NOT_EQUAL()) do 
        val operator := p.previous();
        val rhs := addition(p);
        val op := match operator.kind do 
            case LESS() := Ast.LessThan();
            case LESS_EQUAL() := Ast.LessEqual();
            case EQUAL() := Ast.Equals();
            case NOT_EQUAL() := Ast.NotEquals();
            case GREATER_EQUAL() := Ast.GreaterEqual();
            case GREATER() := Ast.GreaterThan();
        end;
        result := Ast.BinaryOperation(op, result, rhs);
    end;
    result
end

fun addition(p : Parser) : Ast.Expr do 
    var lhs := multiplication(p);
    while !p.at_end() and (p.matches(Tokenizer.PLUS()) or p.matches(Tokenizer.MINUS())) do 
        val op := p.previous().kind;
        val rhs := multiplication(p);
        lhs := Ast.BinaryOperation(match op do 
            case PLUS() := Ast.Add();
            case MINUS() := Ast.Sub();
        end, lhs, rhs);
    end;
    lhs
end

fun multiplication(p : Parser) : Ast.Expr do 
    var lhs := unary(p);
    while !p.at_end() and (p.matches(Tokenizer.STAR()) or p.matches(Tokenizer.SLASH())) do 
        val op := p.previous().kind;
        val rhs := unary(p);
        lhs := Ast.BinaryOperation(match op do 
            case STAR() := Ast.Mul();
            case SLASH() := Ast.Div();
        end, lhs, rhs);
    end;
    lhs
end

fun unary(p : Parser) : Ast.Expr := 
    if p.matches(Tokenizer.MINUS()) then do 
        val operand := unary(p);
        Ast.UnaryOperation(Ast.Minus(), operand)
    end else if p.matches(Tokenizer.PLUS()) then do 
        val operand := unary(p);
        Ast.UnaryOperation(Ast.Plus(), operand)
    end else if p.matches(Tokenizer.BANG()) then do 
        val operand := unary(p);
        Ast.UnaryOperation(Ast.Not(), operand)
    end else do 
        use_expr(p)
    end;

fun use_expr(p : Parser) : Ast.Expr do 
    var lhs := primary(p);
    while p.peek().kind = Tokenizer.LPAREN() or p.peek().kind = Tokenizer.DOT() or p.peek().kind = Tokenizer.LBRACKET() do
        if p.peek().kind = Tokenizer.LPAREN() then do // Consume and test next token
            p.next();
            var args : Ast.Expr[] := [];
            var needs_comma := false;
            while !p.matches(Tokenizer.RPAREN()) do 
                if needs_comma do 
                    p.consume(Tokenizer.COMMA(), "Expect parameters to be separated by commas");
                end;
                args.push(expr(p));
                needs_comma := true;
            end;
            lhs := Ast.FunctionCall(lhs, args);
        end else if p.peek().kind = Tokenizer.LBRACKET() then do
            p.next();
            val idx := expr(p);
            p.consume(Tokenizer.RBRACKET(), "Expect closing ']' in index expression");
            lhs := Ast.IndexExpr(lhs, idx);
        end else do
            p.next();
            val property := p.consume(Tokenizer.IDENTIFIER(), "Expect property name after .");
            lhs := Ast.PropertyExpr(lhs, property.src, Util.LateInitialized.[Ast.AccessType](), Ast.late_type(), Ast.late_type());
        end
    end;
    lhs
end

fun primary(p : Parser) : Ast.Expr := 
    if p.matches(Tokenizer.IF()) then do 
        if_expr(p)    
    end else if p.matches(Tokenizer.DO()) then do 
        block_expr(p)
    end else if p.matches(Tokenizer.FOR()) then do 
        for_loop(p)
    end else if p.matches(Tokenizer.WHILE()) then do 
        while_loop(p)
    end else if p.matches(Tokenizer.MATCH()) then do 
        match_expr(p)
    end else do 
        literal(p)
    end;

fun match_expr(p : Parser) : Ast.Expr do 
    val matched := expr(p);
    p.consume(Tokenizer.DO(), "Expect 'do' after matched expression");
    val cases : (Ast.Pattern, Ast.Expr, Util.LateInitialized(Env.Env(Ast.VarInfo)))[] := [];
    while !p.matches(Tokenizer.END()) do
        p.consume(Tokenizer.CASE(), "Expect 'case' to start pattern");
        val pat := pattern(p);
        p.consume(Tokenizer.ASSIGN(), "Expect ':=' after pattern");
        val result := expr(p);
        p.consume(Tokenizer.SEMICOLON(), "Expect ';' after case arm");
        cases.push((pat, result, Ast.late_env()));
    end;
    Ast.MatchExpr(matched, cases, Ast.late_type(), Ast.late_type())
end

fun pattern(p : Parser) : Ast.Pattern :=
    if p.matches(Tokenizer.NUMBER_LITERAL()) then do 
        val token := p.previous();
        Ast.NumericPattern(number(token.src))
    end else if p.matches(Tokenizer.STRING_LITERAL()) then do 
        val token := p.previous();
        Ast.StringPattern(token.src)
    end else if p.matches(Tokenizer.TRUE()) then do 
        Ast.BooleanPattern(true)
    end else if p.matches(Tokenizer.FALSE()) then do 
        Ast.BooleanPattern(false)
    end else if p.matches(Tokenizer.QUESTION_MARK()) then do 
        Ast.WildcardPattern()
    end else if p.matches(Tokenizer.LPAREN()) then do 
        val parts : Ast.Pattern[] := [];
        var needs_comma := false;
        while !p.matches(Tokenizer.RPAREN()) do 
            if needs_comma do p.consume(Tokenizer.COMMA(), "Expect comma to separate tuple pattern parts") end;
            parts.push(pattern(p));
        end;
        Ast.TuplePattern(parts)
    end else if p.matches(Tokenizer.IDENTIFIER()) then do 
        val name := p.previous().src;
        if p.matches(Tokenizer.LPAREN()) then do 
            val parts : Ast.Pattern[] := [];
            var needs_comma := false;
            while !p.matches(Tokenizer.RPAREN()) do 
                if needs_comma do p.consume(Tokenizer.COMMA(), "Expect comma to separate constructor pattern parameters") end;
                parts.push(pattern(p));
                needs_comma := true;
            end;
            Ast.ConstructorPattern(name, parts)
        end else do 
            Ast.NamePattern(name)
        end
        
    end else do 
        panic("Unexpected "+p.next().kind+", expected start of pattern")
    end;


fun while_loop(p : Parser) : Ast.Expr do
    val cond := expr(p);
    p.consume(Tokenizer.DO(), "Expect 'do' before while loop body");
    val body := block_expr(p);
    Ast.WhileExpr(cond, body, Ast.late_env(), Ast.late_type())
end

fun for_loop(p : Parser) : Ast.Expr do 
    val loop_variable_unconverted := expr(p);
    val loop_variable := convert_setter(loop_variable_unconverted);
    p.consume(Tokenizer.IN(), "Expect 'in' after loop variables");
    val iterated := expr(p);
    p.consume(Tokenizer.DO(), "Expect 'do' before for loop body");
    val body := block_expr(p);
    Ast.ForExpr(loop_variable, iterated, body, Ast.late_env(), Ast.late_type(), Ast.late_type())
end

// Expects that starting do is already consumed
fun block_expr(p : Parser) : Ast.Expr do 
    val stmts : Ast.Stmt[] := [];
    var end_expr := Util.None.[Ast.Expr]();
    var no_end_expr_found := true;
    while no_end_expr_found and !p.matches(Tokenizer.END()) do 
        match block_part(p) do 
            case Statement(st) := do 
                stmts.push(st);
            end;
            case Expression(e) := do 
                end_expr := Util.Some.[Ast.Expr](e);
                p.consume(Tokenizer.END(), "Expect block to end with 'end'");
                no_end_expr_found := false;
            end;
        end;
    end;
    Ast.BlockExpr(stmts, end_expr, Ast.late_type(), Ast.late_env())
end

fun block_part(p : Parser) : Ast.BlockPart do 
    if is_stmt_start(p) then do 
        Ast.Statement(stmt(p))
    end else do 
        val expression := expr(p);
        if p.matches(Tokenizer.SEMICOLON()) then do 
            Ast.Statement(Ast.ExprStmt(expression))
        end else if p.matches(Tokenizer.ASSIGN()) then do 
            Ast.Statement(assign_stmt_to(p, expression))
        end else do 
            Ast.Expression(expression)
        end
    end
end

fun assign_stmt_to(p : Parser, unconverted_setter : Ast.Expr) : Ast.Stmt do 
    val value := expr(p);
    p.consume(Tokenizer.SEMICOLON(), "Expect ':' after assignment");
    val setter := convert_setter(unconverted_setter);
    Ast.SetStatement(setter, value)
end

fun convert_setter(s : Ast.Expr) : Ast.Setter := match s do 
    case Identifier(name) := Ast.Variable(name);
    case TupleExpr(subexprs, lt) := Ast.TupleSetter(for expr in subexprs yield convert_setter(expr) end, lt);
    case PropertyExpr(obj, name, ?, ?, ?) := do 
        Ast.Property(obj, name, Ast.late_type())
    end;
    case ? := panic("Invalid setter: "+s);
end;

fun is_stmt_start(p : Parser) : Bool := is_decl_start(p) or p.peek().kind = Tokenizer.PRINT() or p.peek().kind = Tokenizer.SEMICOLON();

// Expects that if is already consumed
fun if_expr(p : Parser) : Ast.Expr do 
    if p.matches(Tokenizer.VAL()) then do 
        val pat := pattern(p);
        p.consume(Tokenizer.ASSIGN(), "Expect := after pattern of if val expression");
        val matched := expr(p);
        p.consume(Tokenizer.THEN(), "Expect then branch after if val condition");
        val thenBranch := expr(p);
        p.consume(Tokenizer.ELSE(), "Expect else branch in if val expression");
        val elseBranch := expr(p);
        p.wrap_sugared(Ast.IfValExpr(Ast.late_type(), pat, matched, thenBranch, elseBranch, Ast.late_env()))
    end else do 
        val cond := expr(p);
        p.consume(Tokenizer.THEN(), "Expect then branch after if condition");
        val thenBranch := expr(p);
        p.consume(Tokenizer.ELSE(), "Expect else branch in if expression");
        val elseBranch := expr(p);
        Ast.IfExpr(Ast.late_type(), cond, thenBranch, elseBranch)
    end
end

fun literal(p : Parser) : Ast.Expr :=
    if p.matches(Tokenizer.NUMBER_LITERAL()) then do 
        val token := p.previous();
        Ast.NumberLiteral(number(token.src))
    end else if p.matches(Tokenizer.STRING_LITERAL()) then do 
        val token := p.previous();
        Ast.StringLiteral(token.src)
    end else if p.matches(Tokenizer.TRUE()) then do 
        Ast.Boolean(true)
    end else if p.matches(Tokenizer.FALSE()) then do 
        Ast.Boolean(false)
    end else if p.matches(Tokenizer.IDENTIFIER()) then do 
        val token := p.previous();
        Ast.Identifier(token.src)
    end else if p.matches(Tokenizer.NULL()) then do 
        Ast.NullLiteral()
    end else if p.matches(Tokenizer.LPAREN()) then do 
        tuple(p)
    end else if p.matches(Tokenizer.LBRACKET()) then do
        list(p)
    end else if p.matches(Tokenizer.FUN()) then do
        val (params, return_type, body) := end_of_function(p, false);
        Ast.FunctionExpr(params, return_type, body, Ast.late_env())
    end else if p.matches(Tokenizer.VALUE_THIS()) then do 
        Ast.ThisExpr()
    end else do 
        panic("Invalid literal: "+p.peek().kind)
    end;

fun list(p : Parser) : Ast.Expr do
    val parts : Ast.Expr[] := [];
    if !p.matches(Tokenizer.RBRACKET()) do 
        parts.push(expr(p));
        if p.matches(Tokenizer.DOTS()) then do 
            val fst := parts.pop();
            val end_expr := expr(p);
            p.consume(Tokenizer.RBRACKET(), "Expect closing ']' after range expression");
            return Ast.RangeExpr(fst, end_expr);
        end else do 
            while !p.matches(Tokenizer.RBRACKET()) do 
                p.consume(Tokenizer.COMMA(), "Expect ',' to separate list parts");
                parts.push(expr(p));
            end
        end
    end
    Ast.ListExpr(parts, Ast.late_type())
end

fun tuple(p : Parser) : Ast.Expr do 
    val exprs : Ast.Expr[] := [];
    var needs_comma := false;
    while !p.matches(Tokenizer.RPAREN()) do 
        if needs_comma do 
            p.consume(Tokenizer.COMMA(), "Expect ',' to separate tuple parts");
        end
        exprs.push(expr(p));
        needs_comma := true;
    end;
    if exprs.length = 1 then 
        exprs[0]
    else 
        Ast.TupleExpr(exprs, Ast.late_type())
end

fun type_literal(p : Parser) : Ast.Type do
    var left := if p.matches(Tokenizer.NUMBER()) then
        Ast.NumberT()
    else if p.matches(Tokenizer.STRING()) then 
        Ast.StringT()
    else if p.matches(Tokenizer.BOOLEAN()) then 
        Ast.BooleanT()
    else if p.matches(Tokenizer.VOID()) then 
        Ast.VoidT()
    else if p.matches(Tokenizer.TYPE_FUN()) then do 
        p.consume(Tokenizer.LPAREN(), "Expect '(' before parameter types of function type");
        val arg_types : Ast.Type[] := [];
        var needs_comma := false;
        while !p.matches(Tokenizer.RPAREN()) do 
            if needs_comma do 
                p.consume(Tokenizer.COMMA(), "Expect parameter types to be separated by ','");
            end;
            arg_types.push(type_literal(p));
            needs_comma := true;
        end;
        p.consume(Tokenizer.COLON(), "Expect ':' before function return type");
        val ret_type := type_literal(p);
        Ast.Function(arg_types, ret_type)
    end else if p.matches(Tokenizer.LPAREN()) then do 
        val types : Ast.Type[] := [];
        var needs_comma := false;
        while !p.matches(Tokenizer.RPAREN()) do 
            if needs_comma do 
                p.consume(Tokenizer.COMMA(), "Expect tuple type parts to be separated by ','");
            end
            types.push(type_literal(p));
            needs_comma := true;
        end;
        if types.length = 1 then 
            types[0]
        else 
            Ast.Tuple(types)
    end else if p.matches(Tokenizer.IDENTIFIER()) then do 
        Ast.Name(p.previous().src)
    end else do         
        panic("Invalid type: "+p.peek().kind)
    end;

    while p.matches(Tokenizer.LBRACKET()) do
        left := Ast.ListOf(left);
        p.consume(Tokenizer.RBRACKET(), "Expect ']' after '[' in list type");
    end;
    left
end
